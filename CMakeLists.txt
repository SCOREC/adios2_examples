cmake_minimum_required(VERSION 3.13)
project(wdm_coupling VERSION 0.0.1 LANGUAGES CXX Fortran)
set(MPI_CXX_SKIP_MPICXX ON)
find_package(MPI REQUIRED)
#adios2 adds C and Fortran depending on how it was built
find_package(ADIOS2 2.5 REQUIRED)

include(CTest)
if(BUILD_TESTING)
  get_filename_component(COMPILER_DIR "${CMAKE_CXX_COMPILER}" PATH) 
  find_program(MPIRUN NAMES mpirun PATHS "${COMPILER_DIR}")
  message(STATUS "MPIRUN: ${MPIRUN}")
  set(MPIRUN_PROCFLAG "-np" CACHE STRING
      "the command line flag to give process count to MPIRUN")
  message(STATUS "MPIRUN_PROCFLAG: ${MPIRUN_PROCFLAG}")
endif()

function(add_adios2_example exename src)
  add_executable(${exename} ${src})
  target_link_libraries(${exename} adios2::adios2 MPI::MPI_C)
  get_filename_component(ext ${src} EXT)
  if("${ext}" STREQUAL ".F90")
    message(STATUS "fortran!!")
    target_link_libraries(${exename} adios2::adios2_f MPI::MPI_Fortran)
  endif()
endfunction(add_adios2_example)

add_adios2_example(helloSstWriter app1/helloSstWriter.cpp)
add_adios2_example(app1_ftn       app1/app1.F90)
add_adios2_example(app1_cpp       app1/app1.cc)
add_adios2_example(app1_2d_cpp    app1/app1_2d.cc)
add_adios2_example(helloSstReader app2/helloSstReader.cpp)
add_adios2_example(app2_cpp       app2/app2.cc)
add_adios2_example(app2_2d_cpp    app2/app2_2d.cc)
